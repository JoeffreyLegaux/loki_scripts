!OPTIONS XOPT(NOEVAL)
SUBROUTINE RADAER ( YDEAERD,YDERAD,YDPHY,KIDIA , KFDIA , KLON , KLEV,&
 & PAPRS , PAPRSF, PT    , PTS,&
 & PAESEA, PAELAN, PAESOO, PAEDES, PAESUL, PAEVOL,&
 & PAER, PAERINDS                        )  

! ======================================================================

!**** *RADAER  - COMPUTES DISTRIBUTION OF AEROSOLS

!     PURPOSE.
!     --------

!     - USE THE ECMWF CLIMATOLOGICAL AEROSOLS DISTRIBUTION
!                   SEE TANRE ET AL., 1984
!     - AVOID THE USE OF RADACT WHICH DOES NOT WORK ON A
!                   NON-ROTATED, UNSTRETCHED GRID.

!     WARNING.
!     --------

!     RADAER HAS BEEN TESTED FOR ARPEGE-CLIMAT ONLY. IN PARTICULAR
!     ONE SHOULD BE CAREFUL WHEN USING IT WITH SPACE INTERPOLATION
!     OR MEMORY PARTITION (SEE RADINT).

!**   INTERFACE.
!     ----------

!     CALL *RADAER* FROM *APLPAR*

!        EXPLICIT ARGUMENTS :
!        --------------------

!        ==== INPUTS ===

! KIDIA  : LATITUDE INDEX OF 1-ST LATITUDE BAND IN VECTOR
! KFDIA  : LATITUDE INDEX OF LAST LATITUDE BAND IN VECTOR
! KLON   : VECTOR LENGTH
! KLEV   : NUMBER OF LEVELS
! PAPRS  : (KLON,KLEV+1)      ; HALF LEVEL PRESSURE
! PAPRSF : (KLON,KLEV )       ; FULL LEVEL PRESSURE
! PT     : (KLON,KLEV)        ; FULL LEVEL TEMPERATURE
! PTS    : (KLON)             ; SURFACE TEMPERATURE
! PAESOO : (KLON)             ; SOOT AEROSOLS
! PAELAN : (KLON)             ; LAND AEROSOLS
! PAESEA : (KLON)             ; SEA AEROSOLS
! PAEDES : (KLON)             ; DESERT AEROSOLS
! PAESUL : (KLON)             ; SULFAT AEROSOLS
! PAEVOL : (KLON)             ; VOLCANO AEROSOLS

!        ==== OUTPUT ===

! PAER   : (KLON,KLEV,6)      ; OPTICAL THICKNESS OF THE AEROSOLS
! PAERINDS : (KLON,KLEV)      ; OPTICAL THICKNESS OF THE SULFATE AEROSOL
!                               ; USED FOR COMPUTATION OF ITS INDIRECT EFFECT

!        IMPLICIT ARGUMENTS 
!        --------------------

!        NONE

!     METHOD.
!     -------

!     LAND, SEA, SOOT AND DESERT AEROSOLS DISTRIBUTIONS ARE RED
!     FROM A FILE CONTAINING VARIOUS CLIMATOLOGIES AND COMBINED
!     HERE WITH VERTICAL PROFILES TO GET THE 3D DISTRIBUTION.

!     EXTERNALS.
!     ----------

!     NONE

!     REFERENCE.
!     ----------

!     SEE RADIATION'S PART OF THE MODEL'S DOCUMENTATION AND
!     ECMWF RESEARCH DEPARTMENT DOCUMENTATION OF THE "I.F.S"
!     FOR RADACT
!     J.-J. MORCRETTE E.C.M.W.F.   91/03/15 ORIGINAL RADACT SUBROUTINE

!     AUTHOR.
!     -------

!     Ph. DANDIN      METEO-FRANCE 94/11/14 AEROSOLS OUT OF RADINT

!     MODIFICATIONS.
!     --------------
!     M.Hamrud      01-Oct-2003 CY28 Cleaning
!     04/09 : F. BOUYSSEL - USE OF TEGEN'S AEROSOLS (LNEWAER)
!     09/09 : A.Alias  - SULFAT AEROSOLS (combined to land) (J.F.Gueremy)
!                      - PAEVOL  put in stratospheric aerosols category 
!                                instead of  RCSTBGA=cste (A.Voldoire)
!     09/07 : A. VOLDOIRE  - Add a table for sulfate aerosols alone 
!                            used for indirect effect calculation only
!                            For the direct effect, sulfate aerosols are 
!                            mixed with Land aerosols
!-----------------------------------------------------------------------

USE PARKIND1, ONLY : JPIM, JPRB
USE YOMHOOK , ONLY : DR_HOOK, JPHOOK, LHOOK
USE YOEAERD , ONLY : TEAERD
USE YOERAD  , ONLY : TERAD
USE YOMPHY  , ONLY : TPHY

IMPLICIT NONE

TYPE(TEAERD)       ,INTENT(IN)   :: YDEAERD
TYPE(TERAD)        ,INTENT(IN)   :: YDERAD
TYPE(TPHY)         ,INTENT(IN)   :: YDPHY
INTEGER(KIND=JPIM) ,INTENT(IN)   :: KIDIA
INTEGER(KIND=JPIM) ,INTENT(IN)   :: KFDIA 
INTEGER(KIND=JPIM) ,INTENT(IN)   :: KLON 
INTEGER(KIND=JPIM) ,INTENT(IN)   :: KLEV 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PAPRS(KLON,KLEV+1) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PT(KLON,KLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PTS(KLON) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PAESEA(KLON) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PAELAN(KLON) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PAESOO(KLON) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PAEDES(KLON) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PAESUL(KLON)
REAL(KIND=JPRB)    ,INTENT(IN)   :: PAEVOL(KLON)
REAL(KIND=JPRB)    ,INTENT(OUT)  :: PAER(KLON,KLEV,6) 
REAL(KIND=JPRB)    ,INTENT(OUT)  :: PAERINDS(KLON,KLEV) 
!     -----------------------------------------------------------------

!*       0.1   ARGUMENTS.
!              ----------

!     -----------------------------------------------------------------

!*       0.2   LOCAL ARRAYS.
!              -------------

REAL(KIND=JPRB) :: ZTH(KLON,KLEV+1)

REAL(KIND=JPRB) :: ZDPN  (KLON),ZDPO  (KLON)
REAL(KIND=JPRB) :: ZAEQSN(KLON),ZAEQSO(KLON)
REAL(KIND=JPRB) :: ZAEQLN(KLON),ZAEQLO(KLON)
REAL(KIND=JPRB) :: ZAEQUN(KLON),ZAEQUO(KLON)
REAL(KIND=JPRB) :: ZAEQDN(KLON),ZAEQDO(KLON)
REAL(KIND=JPRB) :: ZAETRN(KLON),ZAETRO(KLON)
REAL(KIND=JPRB) :: ZAEQFN(KLON),ZAEQFO(KLON)
REAL(KIND=JPRB) :: ZTOT(KLON)

INTEGER(KIND=JPIM) :: JLEV, JLON, JAER

REAL(KIND=JPRB) :: ZAETR, ZDPNMO, ZGRTH, ZREPAER
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

!*         1.     AEROSOL PARAMETERS COMPUTATIONS
!                 -------------------------------

! Aerosols come from files containing the climatologies.

!*         2.      VERTICAL DISTRIBUTION
!*                 ---------------------

!*         2.1    HALF-LEVEL TEMPERATURE
!*          =     TEMPERATURES AT LAYERS' BOUNDARIES.

! Half-level temperature interpolation weighted by pressure 
! just as in RADINT (loop 1222) but shortened...

IF (LHOOK) CALL DR_HOOK('RADAER',0,ZHOOK_HANDLE)
ASSOCIATE(CVDAED=>YDEAERD%CVDAED, CVDAEL=>YDEAERD%CVDAEL, &
 & CVDAES=>YDEAERD%CVDAES, CVDAEU=>YDEAERD%CVDAEU, RCAEOPD=>YDEAERD%RCAEOPD, &
 & RCAEOPL=>YDEAERD%RCAEOPL, RCAEOPS=>YDEAERD%RCAEOPS, RCAEOPU=>YDEAERD%RCAEOPU, &
 & RCSTBGA=>YDEAERD%RCSTBGA, RCTRBGA=>YDEAERD%RCTRBGA, RCTRPT=>YDEAERD%RCTRPT, &
 & RCVOBGA=>YDEAERD%RCVOBGA, &
 & LNEWAER=>YDERAD%LNEWAER, LAEROVOL=>YDPHY%LAEROVOL, LAEROSUL=>YDPHY%LAEROSUL)
ZREPAER=1.E-12_JPRB
DO JLEV=2,KLEV
  DO JLON=KIDIA,KFDIA
    ZTH(JLON,JLEV)=(PT(JLON,JLEV-1)*PAPRSF(JLON,JLEV-1)&
     & *(PAPRSF(JLON,JLEV)-PAPRS(JLON,JLEV))&
     & +PT(JLON,JLEV)*PAPRSF(JLON,JLEV)*(PAPRS(JLON,JLEV)-PAPRSF(JLON,JLEV-1)))&
     & *(1.0_JPRB/(PAPRS(JLON,JLEV)*(PAPRSF(JLON,JLEV)-PAPRSF(JLON,JLEV-1))))  
  ENDDO
ENDDO

DO JLON=KIDIA,KFDIA
  ZTH(JLON,KLEV+1)=PTS(JLON)
  ZTH(JLON,1)=PT(JLON,1)-PAPRSF(JLON,1)*(PT(JLON,1)-ZTH(JLON,2))&
   & /(PAPRSF(JLON,1)-PAPRS(JLON,2))  
ENDDO

!*         2.2    VERTICAL DISTRIBUTIONS

DO JLON=KIDIA,KFDIA
  ZDPO(JLON)=PAPRS (JLON,1)
  IF (LNEWAER) THEN
    ZAEQSO(JLON)=PAESEA(JLON)*CVDAES(1)
    IF (LAEROSUL) THEN
      ZAEQLO(JLON)=(PAELAN(JLON)+PAESUL(JLON))*CVDAEL(1)
      ZAEQFO(JLON)=PAESUL(JLON)*CVDAEL(1)
    ELSE
      ZAEQLO(JLON)=PAELAN(JLON)*CVDAEL(1)
      ZAEQFO(JLON)=0._JPRB
    ENDIF
    ZAEQUO(JLON)=PAESOO(JLON)*CVDAEU(1)
    ZAEQDO(JLON)=PAEDES(JLON)*CVDAED(1)
  ELSE
    ZAEQSO(JLON)=RCAEOPS*PAESEA(JLON)*CVDAES(1)
    ZAEQLO(JLON)=RCAEOPL*PAELAN(JLON)*CVDAEL(1)
    ZAEQUO(JLON)=RCAEOPU*PAESOO(JLON)*CVDAEU(1)
    ZAEQDO(JLON)=RCAEOPD*PAEDES(JLON)*CVDAED(1)
  ENDIF
  ZAETRO(JLON)=1.0_JPRB
  ZTOT(JLON)=0.0_JPRB
ENDDO

DO JLEV=1,KLEV
  DO JLON=KIDIA,KFDIA
    ZGRTH= ZTH(JLON,JLEV)/ZTH(JLON,JLEV+1)
    ZDPN(JLON)=PAPRS (JLON,JLEV+1)

    IF (LNEWAER) THEN
      ZAEQSN(JLON)=PAESEA(JLON)*CVDAES(JLEV+1)
      IF (LAEROSUL) THEN
        ZAEQLN(JLON)=(PAELAN(JLON)+PAESUL(JLON))*CVDAEL(JLEV+1)
        ZAEQFN(JLON)=PAESUL(JLON)*CVDAEL(JLEV+1)
      ELSE
        ZAEQLN(JLON)=PAELAN(JLON)*CVDAEL(JLEV+1)
        ZAEQFN(JLON)=0._JPRB
      ENDIF
      ZAEQUN(JLON)=PAESOO(JLON)*CVDAEU(JLEV+1)
      ZAEQDN(JLON)=PAEDES(JLON)*CVDAED(JLEV+1)
    ELSE
      ZAEQSN(JLON)=RCAEOPS*PAESEA(JLON)*CVDAES(JLEV+1)
      ZAEQLN(JLON)=RCAEOPL*PAELAN(JLON)*CVDAEL(JLEV+1)
      ZAEQUN(JLON)=RCAEOPU*PAESOO(JLON)*CVDAEU(JLEV+1)
      ZAEQDN(JLON)=RCAEOPD*PAEDES(JLON)*CVDAED(JLEV+1)
    ENDIF

    IF (0.5_JPRB*(PAPRS(JLON,JLEV)+PAPRS(JLON,JLEV+1)) < 999._JPRB) THEN
! for models with top above 10hPa
      ZAETRN(JLON)=1.0_JPRB
      ZAETRO(JLON)=1.0_JPRB
    ELSE
      ZAETRN(JLON)=ZAETRO(JLON)*(MIN(1.0_JPRB,ZGRTH))**RCTRPT
    ENDIF

    ZAETR=SQRT  (ZAETRN(JLON)*ZAETRO(JLON))
    ZDPNMO    =ZDPN(JLON)-ZDPO(JLON)
    ZTOT(JLON) = ZTOT(JLON) + ZAETR * ZDPNMO
    IF (LNEWAER) THEN
      PAERINDS(JLON,JLEV)=(1.0_JPRB-ZAETR)*(ZAEQFN(JLON)-ZAEQFO(JLON))
    ENDIF
    PAER(JLON,JLEV,1)=(1.0_JPRB-ZAETR)*(RCTRBGA*ZDPNMO+ ZAEQLN(JLON)-ZAEQLO(JLON))
    PAER(JLON,JLEV,2)=(1.0_JPRB-ZAETR)*(ZAEQSN(JLON)-ZAEQSO(JLON))
    PAER(JLON,JLEV,3)=(1.0_JPRB-ZAETR)*(ZAEQDN(JLON)-ZAEQDO(JLON))
    PAER(JLON,JLEV,4)=(1.0_JPRB-ZAETR)*(ZAEQUN(JLON)-ZAEQUO(JLON))
    PAER(JLON,JLEV,5)= ZAETR * RCVOBGA*ZDPNMO
    IF (LAEROVOL) THEN
      PAER(JLON,JLEV,6)= ZAETR * ZDPNMO * PAEVOL(JLON)
    ELSE 
      PAER(JLON,JLEV,6)= ZAETR * RCSTBGA*ZDPNMO
    ENDIF
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZDPO(JLON)=ZDPN(JLON)
    ZAEQSO(JLON)=ZAEQSN(JLON)
    ZAEQLO(JLON)=ZAEQLN(JLON)
    ZAEQUO(JLON)=ZAEQUN(JLON)
    ZAEQDO(JLON)=ZAEQDN(JLON)
    ZAETRO(JLON)=ZAETRN(JLON)
  ENDDO
  IF (LNEWAER) THEN
    DO JLON=KIDIA,KFDIA
      ZAEQFO(JLON)=ZAEQFN(JLON)
    ENDDO
  ENDIF
ENDDO

IF (LAEROVOL) THEN
  DO JLON=KIDIA,KFDIA
    PAER(JLON,:,6) = PAER(JLON,:,6) / ZTOT(JLON)
  ENDDO
ENDIF

DO JLEV=1,KLEV
  DO JAER=1,6
    DO JLON=KIDIA,KFDIA
      PAER(JLON,JLEV,JAER)=MAX(PAER(JLON,JLEV,JAER),ZREPAER)
    ENDDO
  ENDDO
ENDDO

IF (LNEWAER) THEN
  DO JLEV=1,KLEV
    DO JLON=KIDIA,KFDIA
       PAERINDS(JLON,JLEV)=MAX(PAERINDS(JLON,JLEV),ZREPAER)
    ENDDO
  ENDDO
ENDIF
!     ------------------------------------------------------------------

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('RADAER',1,ZHOOK_HANDLE)
END SUBROUTINE RADAER

