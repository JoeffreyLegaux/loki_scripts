SUBROUTINE ACUPTQ ( YDCST, KLON, KIDIA, KFDIA, KFLEV, KNBTRA, LDPTQ, PFRSO, PFRTH, PDIFCQ, PDIFCS, &
& PDIFTQ, PDIFTS, PFCCQL, PFCCQN, PFPLCL, PFPLCN, PFECL, PFECN, PFACL, PFACN, PRDELP, PT, PQ,  &
& PTS, PTENDH, PTENDQ, PDIFTTRA, PTENTRA_STR )

!     ------------------------------------------------------------------
!     EVOLUTION DE (T,Qv) APRES TURBULENCE ET CONVECTION
!     ------------------------------------------------------------------

!     ARGUMENTS D ENTREE
!     ------------------
! KLON       : DIMENSION HORIZONTALE
! KIDIA      : DEBUT DE LA BOUCLE HORIZONTALE
! KFDIA      : BORNE HORIZONTALE DES CALCULS
! KFLEV      : DIMENSION ET BORNE VERTICALE
! KNBTRA     : NOMBRE DE TRACEURS (SCALAIRES PASSIFS, AEROSOL, ESPECES CHIMIQUES)

! --- INPUT 2D (0:KLEV).
!     -----------------.
! PFRSO      : SOLAR FLUX
! PFRTH      : THERMAL FLUX
! PDIFCQ     : FLUX CONVECTIF D'HUMIDITE SPECIFIQUE (HORS COND. ET RR).
! PDIFCS     : FLUX CONVECTIF D'ENTHALPIE .
! PDIFTQ     : FLUX TURBULENT (ET Q NEGATIF) D'HUMIDITE SPECIFIQUE.
! PDIFTS     : FLUX TURBULENT D'ENTHALPIE (OU D'ENERGIE STATIQUE SECHE).
! PDIFTTRA   : FLUX TURBULENT DES TRACEURS (SCALAIRES PASSIFS, AEROSOL, ESPECES CHIMIQUES).
! PFCCQL     : FLUX DE CONDENSATION LIE AUX RR CONVECTIVES LIQUIDES.
! PFCCQN     : FLUX DE CONDENSATION LIE AUX RR CONVECTIVES NEIGEUSES.
! PFPLCL     : PRECIPITATIONS CONVECTIVES SOUS FORME LIQUIDE.
! PFPLCN     : PRECIPITATIONS CONVECTIVES SOUS FORME NEIGE.
! PFECL      : FLUX DE PRECIPITATIONS DU A L'EVAPORATION CONVECTIVE.
! PFECN      : FLUX DE PRECIPITATIONS DU A LA SUBLIMATION CONVECTIVE.
! PFACL      : FLUX DE PRECIPITATION CONVECTIVE: TERME DE GENERATION/FORMATION.
! PFACN      : FLUX DE PRECIPITATION CONVECTIVE: TERME DE GENERATION/FORMATION.

! --- INPUT 2D (1:KLEV).
!     -----------------.
! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.
! PT         : TEMPERATURE.
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.

! - 1D.
!   --.
! PTS        : TEMPERATURE DE SURFACE.

! --- SORTIES 2D (1:KFLEV).
!     --------------------.
! PTENDH     : TENDANCE DE L'ENTHALPIE.
! PTENDQ     : TENDANCE DE L'HUMIDITE.

! --- SORTIES 3D (KPROMA,1:KFLEV,KNBTRA).
!     ---------------------------------.
! PTENTRA_STR : TENDANCES DES TRACEURS, tendances calculees avec sorties de acdifv2, donc dans le cas
!              de convection Tiedtke les tendances sont calculées à partir de flux turbulents uniquement. PTENTRA_STR INOUT de CUCALLN_MF 

! --- AUTEUR.
!     ------.
!     25/10/10 F.BOUYSSEL.

! --- MODIFICATION.
!     ------------.
!     10/07/19 Y.BOUTELOUP : Add PFRSO and PFRTH for Tiedtke/Bechtold scheme     
!     M. Michou & M. Cussac : Add tracers tendency calculation before convection
!     R. El Khatib 22-Jun-2022 A contribution to simplify phasing after the refactoring of YOMCLI/YOMCST/YOETHF.
!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK, JPHOOK

USE YOMCST   , ONLY : TCST

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(TCST)         ,INTENT(IN)   :: YDCST
INTEGER(KIND=JPIM) ,INTENT(IN)   :: KLON 
INTEGER(KIND=JPIM) ,INTENT(IN)   :: KIDIA 
INTEGER(KIND=JPIM) ,INTENT(IN)   :: KFDIA 
INTEGER(KIND=JPIM) ,INTENT(IN)   :: KFLEV 
INTEGER(KIND=JPIM) ,INTENT(IN)    :: KNBTRA
LOGICAL            ,INTENT(IN)   :: LDPTQ
REAL(KIND=JPRB)    ,INTENT(IN)   :: PFRSO(KLON,0:KFLEV)
REAL(KIND=JPRB)    ,INTENT(IN)   :: PFRTH(KLON,0:KFLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PDIFCQ(KLON,0:KFLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PDIFCS(KLON,0:KFLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PDIFTQ(KLON,0:KFLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PDIFTS(KLON,0:KFLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PFCCQL(KLON,0:KFLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PFCCQN(KLON,0:KFLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PFPLCL(KLON,0:KFLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PFPLCN(KLON,0:KFLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PFECL(KLON,0:KFLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PFECN(KLON,0:KFLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PFACL(KLON,0:KFLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PFACN(KLON,0:KFLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PRDELP(KLON,KFLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PT(KLON,KFLEV) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PQ(KLON,KFLEV)
REAL(KIND=JPRB)    ,INTENT(IN)   :: PTS(KLON) 
REAL(KIND=JPRB)    ,INTENT(OUT)  :: PTENDH(KLON,KFLEV) 
REAL(KIND=JPRB)    ,INTENT(OUT)  :: PTENDQ(KLON,KFLEV)
REAL(KIND=JPRB)    ,INTENT(IN)  , OPTIONAL   :: PDIFTTRA(KLON,0:KFLEV,KNBTRA)  
REAL(KIND=JPRB)    ,INTENT(OUT) , OPTIONAL   :: PTENTRA_STR (KLON,KFLEV,KNBTRA)
!-----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZGSDP (KLON,KFLEV)
REAL(KIND=JPRB) :: ZJTOT (KLON,0:KFLEV)
REAL(KIND=JPRB) :: ZTI   (KLON,0:KFLEV)

INTEGER(KIND=JPIM) :: JLEV, JLON, JTRA

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACUPTQ',0,ZHOOK_HANDLE)
ASSOCIATE(RCPD=>YDCST%RCPD, RCS=>YDCST%RCS, RCW=>YDCST%RCW, RG=>YDCST%RG, RLSZER=>YDCST%RLSZER, &
 & RLVZER=>YDCST%RLVZER)
!-----------------------------------------------------------------------

! Calculation of temperatures on half levels

DO JLON = KIDIA, KFDIA
  ZTI(JLON,0)=PT(JLON,1)
  ZTI(JLON,KFLEV)=PTS(JLON)
ENDDO

DO JLEV= 1, KFLEV-1
  DO JLON = KIDIA, KFDIA
    ZTI(JLON,JLEV)=(PT(JLON,JLEV)+PT(JLON,JLEV+1))*0.5_JPRB
  ENDDO
ENDDO

! Computation of the total enthalpy flux J_tot

DO JLEV= 0, KFLEV
  DO JLON = KIDIA, KFDIA
     IF (LDPTQ) THEN
        ZJTOT(JLON,JLEV) = PFRSO(JLON,JLEV)+PFRTH(JLON,JLEV)+&
            & PDIFTS(JLON,JLEV)+PDIFCS(JLON,JLEV)+&
            & (RCW-RCPD)*PFPLCL(JLON,JLEV)*ZTI(JLON,JLEV)+&
            & (RCS-RCPD)*PFPLCN(JLON,JLEV)*ZTI(JLON,JLEV)-&
            & RLVZER*(PFCCQL(JLON,JLEV)-PFECL(JLON,JLEV))-&
            & RLSZER*(PFCCQN(JLON,JLEV)-PFECN(JLON,JLEV))
      ELSE      
        ZJTOT(JLON,JLEV) = PFRSO(JLON,JLEV)+PFRTH(JLON,JLEV)+&
            & PDIFTS(JLON,JLEV)
      ENDIF      
  ENDDO
ENDDO

!CDIR UNROLL=8
DO JLEV = 1, KFLEV
  DO JLON = KIDIA, KFDIA

    ZGSDP(JLON,JLEV)=RG*PRDELP(JLON,JLEV)

    IF (LDPTQ) THEN
       PTENDQ(JLON,JLEV) = ZGSDP(JLON,JLEV)*(&
        & (PDIFTQ (JLON,JLEV-1) - PDIFTQ (JLON,JLEV)) +&
        & (PDIFCQ (JLON,JLEV-1) - PDIFCQ (JLON,JLEV)) +&
        & (PFCCQL (JLON,JLEV-1) - PFCCQL (JLON,JLEV)) +&
        & (PFCCQN (JLON,JLEV-1) - PFCCQN (JLON,JLEV)) -&
        & (PFECL  (JLON,JLEV-1) - PFECL  (JLON,JLEV)) -&
        & (PFECN  (JLON,JLEV-1) - PFECN  (JLON,JLEV)))
       PTENDH(JLON,JLEV) = ZGSDP(JLON,JLEV)*(ZJTOT  (JLON,JLEV-1) - ZJTOT  (JLON,JLEV)) 
    ELSE    
       PTENDQ(JLON,JLEV) = MAX(0._JPRB,ZGSDP(JLON,JLEV)*(PDIFTQ (JLON,JLEV-1) - PDIFTQ (JLON,JLEV)))
       PTENDH(JLON,JLEV) = MAX(0._JPRB,ZGSDP(JLON,JLEV)*(ZJTOT  (JLON,JLEV-1) - ZJTOT  (JLON,JLEV)))
    ENDIF   

  ENDDO
ENDDO

IF (KNBTRA > 0) THEN
   IF (.NOT.LDPTQ)  THEN ! LDPTQ case specific to PNT not used in climate 
      DO JLEV = 1, KFLEV
         DO JLON = KIDIA, KFDIA
            ZGSDP(JLON,JLEV)=RG*PRDELP(JLON,JLEV)
            DO JTRA = 1, KNBTRA 
               PTENTRA_STR(JLON,JLEV,JTRA) = ZGSDP(JLON,JLEV)*(PDIFTTRA (JLON,JLEV-1,JTRA) - PDIFTTRA (JLON,JLEV,JTRA))
            ENDDO
         ENDDO
      ENDDO
    ENDIF 
ENDIF

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACUPTQ',1,ZHOOK_HANDLE)
END SUBROUTINE ACUPTQ

