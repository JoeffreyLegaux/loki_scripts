SUBROUTINE GPINISLB_PART3_EXPL (YDGEOMETRY, YDDYNA, KST, KEND, PVVEL0, &
& PZPRE0F, PB2VVEL, PB2GWF, PB2GDW, PB2GWS, PGWFT0, PGDW0, PGWS0)

!**** *GPINISLB_PART3_EXPL*

!     Author.
!     -------
!      Philippe Marguinaud *METEO-FRANCE*
!      Original : 28-11-2022

USE PARKIND1, ONLY : JPIM, JPRB
USE YOMHOOK, ONLY : DR_HOOK, LHOOK, JPHOOK

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE YOMDYNA      , ONLY : TDYNA


IMPLICIT NONE

TYPE(GEOMETRY)     ,INTENT(IN)               :: YDGEOMETRY
TYPE(TDYNA)        ,INTENT(IN)               :: YDDYNA
INTEGER(KIND=JPIM) ,INTENT(IN)               :: KST
INTEGER(KIND=JPIM) ,INTENT(IN)               :: KEND
REAL(KIND=JPRB)    ,INTENT(IN)               :: PVVEL0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)    ,INTENT(IN)               :: PZPRE0F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)    ,INTENT(INOUT)            :: PB2VVEL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)    ,INTENT(INOUT)            :: PB2GWF(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)    ,INTENT(INOUT)            :: PB2GDW(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)    ,INTENT(INOUT)            :: PB2GWS(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)    ,INTENT(IN)    ,OPTIONAL  :: PGWFT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)    ,INTENT(IN)    ,OPTIONAL  :: PGDW0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)    ,INTENT(IN)    ,OPTIONAL  :: PGWS0(YDGEOMETRY%YRDIM%NPROMA)

#include "abor1.intfb.h"

INTEGER (KIND=JPIM) :: JLEV

REAL (KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GPINISLB_PART3_EXPL',0,ZHOOK_HANDLE)

!*          2.     PB2[X] for other purposes.
!                  -------------------------- 

!*          2.1    PB2VVEL.

!cf this do loop is furiously non linear, but useless in tl model
  ! Non linear: TL code is in CPG_GP_TL.
DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
  PB2VVEL(KST:KEND,JLEV)=PVVEL0(KST:KEND,JLEV)*PZPRE0F(KST:KEND,JLEV)
ENDDO

!*          2.2    (gw), (g w_surf) and (g dw).

IF (YDDYNA%LNHDYN.AND.YDDYNA%LGWADV.AND.YDGEOMETRY%YRVERT_GEOM%YRCVER%LVERTFE.AND.YDGEOMETRY%YRVERT_GEOM%YRCVER%LVFE_GW) THEN
  ! Not yet coded in the TL code.
  ! Makes sence only for LTWOTL=T and LSLAG=T
  IF (.NOT.(PRESENT(PGWFT0).AND.PRESENT(PGDW0).AND.PRESENT(PGWS0)))&
   & CALL ABOR1(' GPINISLB 2a: missing optional input arguments!')
  DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
    PB2GWF(KST:KEND,JLEV)=PGWFT0(KST:KEND,JLEV)
    PB2GDW(KST:KEND,JLEV)=PGDW0(KST:KEND,JLEV)
  ENDDO
  PB2GWS(KST:KEND)=PGWS0(KST:KEND)
ENDIF

IF (LHOOK) CALL DR_HOOK('GPINISLB_PART3_EXPL',1,ZHOOK_HANDLE)

END SUBROUTINE GPINISLB_PART3_EXPL
