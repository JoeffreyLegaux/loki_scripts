SUBROUTINE MEANWIND_XFU (KLON, KIDIA, KFDIA, KMEANSTEPS, PXFU, PXFV, PXU, PXV, PMWINDCALC, ZUM, ZVM)

USE PARKIND1         , ONLY : JPIM, JPRB
USE YOMHOOK          , ONLY : DR_HOOK, LHOOK, JPHOOK

! The algorithm is the same as the one used to compute observed mean wind

IMPLICIT NONE

INTEGER(KIND=JPIM)     ,INTENT(IN)     :: KLON
INTEGER(KIND=JPIM)     ,INTENT(IN)     :: KIDIA
INTEGER(KIND=JPIM)     ,INTENT(IN)     :: KFDIA
INTEGER(KIND=JPIM)     ,INTENT(IN)     :: KMEANSTEPS
REAL(KIND=JPRB)        ,INTENT(IN)     :: PXFU(KLON)
REAL(KIND=JPRB)        ,INTENT(IN)     :: PXFV(KLON)
REAL(KIND=JPRB)        ,INTENT(IN)     :: PXU(KLON)
REAL(KIND=JPRB)        ,INTENT(IN)     :: PXV(KLON)
REAL(KIND=JPRB)        ,INTENT(INOUT)  :: PMWINDCALC(KLON)
REAL(KIND=JPRB)        ,INTENT(OUT)    :: ZUM(KLON)
REAL(KIND=JPRB)        ,INTENT(OUT)    :: ZVM(KLON)

REAL(KIND=JPRB) :: ZMEANMODULE(KLON)
REAL(KIND=JPRB) :: ZMEANANGLE
REAL(KIND=JPRB) :: ZANGLE
REAL(KIND=JPRB) :: ZMEANCANGLE
REAL(KIND=JPRB) :: ZMEANSANGLE
REAL(KIND=JPRB), PARAMETER :: ZEPS = 1.E-12_JPRB

INTEGER (KIND=JPIM) :: JLON

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('MEANWIND_XFU', 0, ZHOOK_HANDLE)

IF (KMEANSTEPS==1) THEN
  ZUM(KIDIA:KFDIA)=PXU(KIDIA:KFDIA)
  ZVM(KIDIA:KFDIA)=PXV(KIDIA:KFDIA)
  PMWINDCALC(KIDIA:KFDIA)=1.0_JPRB
ELSE
  DO JLON=KIDIA,KFDIA
    ! Module of mean wind is the mean of the modules
    ZMEANMODULE(JLON)=SQRT(PXFU(JLON)**2+PXFV(JLON)**2)
  ENDDO
  DO JLON=KIDIA,KFDIA
    ! Module of mean wind is the mean of the modules
    ZMEANMODULE(JLON)= ZMEANMODULE(JLON)*REAL(KMEANSTEPS-1,JPRB) + SQRT(PXU(JLON)**2+PXV(JLON)**2)
  ENDDO
  DO JLON=KIDIA,KFDIA
    ! We compute mean of COS and mean of SIN
    ! Direction of mean wind is the direction of vector [mean COS, mean SIN]
    IF (ABS(PXFV(JLON))<ZEPS.AND.ABS(PXFU(JLON))<ZEPS) THEN
      ZMEANANGLE=0.0_JPRB
    ELSE
      ZMEANANGLE=ATAN2(PXFV(JLON),PXFU(JLON))
    ENDIF
    IF (ABS(PXV(JLON))<ZEPS.AND.ABS(PXU(JLON))<ZEPS) THEN
      ZANGLE=0.0_JPRB
    ELSE
      ZANGLE=ATAN2(PXV(JLON),PXU(JLON))
    ENDIF
    ZMEANCANGLE=( COS(ZMEANANGLE)*REAL(KMEANSTEPS-1,JPRB) * &
     & PMWINDCALC(JLON)+ &
     & COS(ZANGLE) ) / REAL(KMEANSTEPS,JPRB)
    ZMEANSANGLE=( SIN(ZMEANANGLE)*REAL(KMEANSTEPS-1,JPRB) * &
     & PMWINDCALC(JLON)+ &
     & SIN(ZANGLE) ) / REAL(KMEANSTEPS,JPRB)
    PMWINDCALC(JLON)=SQRT(ZMEANCANGLE**2+ZMEANSANGLE**2)
    IF (ABS(ZMEANSANGLE)<ZEPS.AND.ABS(ZMEANCANGLE)<ZEPS) THEN
      ZMEANANGLE=0.0_JPRB
    ELSE
      ZMEANANGLE=ATAN2(ZMEANSANGLE,ZMEANCANGLE)
    ENDIF
    ZUM(JLON)=ZMEANMODULE(JLON)*COS(ZMEANANGLE)/ REAL(KMEANSTEPS,JPRB)
    ZVM(JLON)=ZMEANMODULE(JLON)*SIN(ZMEANANGLE)/ REAL(KMEANSTEPS,JPRB)
  ENDDO
ENDIF

IF (LHOOK) CALL DR_HOOK('MEANWIND_XFU', 1, ZHOOK_HANDLE)

END SUBROUTINE MEANWIND_XFU
